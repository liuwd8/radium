# Copyright 2024 The Chromium Radium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//build/config/win/console_app.gni")
import("//build/config/win/manifest.gni")
import("//radium/radium_paks.gni")
import("//tools/resources/generate_resource_allowlist.gni")

assert(!is_ios, "Chromium/iOS shouldn't use anything in //chrome")

if (is_win && enable_resource_allowlist_generation) {
  _radium_resource_allowlist = "$target_gen_dir/chrome_resource_allowlist.txt"
}

if (is_win) {
  action("reorder_imports") {
    script = "//radium/tools/win/reorder-imports.py"

    # See comment in radium_dll.gypi in the hardlink_to_output
    # target for why this cannot be 'initial' like the DLL.
    inputs = [ "$root_out_dir/initialexe/radium.exe" ]
    outputs = [
      "$root_out_dir/radium.exe",
      "$root_out_dir/radium.exe.pdb",
    ]
    args = [
      "-i",
      rebase_path("$root_out_dir/initialexe", root_build_dir),
      "-o",
      rebase_path("$root_out_dir", root_build_dir),
      "-a",
      current_cpu,
    ]
    deps = [ ":radium_exe" ]
  }
}

group("all") {
  deps = [ ":radium" ]
}

group("radium") {
  deps = []

  if (is_android) {
    deps += [ "//radium/android:radium_apk" ]
  } else if (is_win) {
    public_deps = [ ":radium_exe" ]
    data_deps = [ ":radium_exe" ]

    public_deps += [ ":reorder_imports" ]
    data_deps += [ ":reorder_imports" ]
  } else {
    deps += [ ":radium_exe" ]
  }
}

executable("radium_exe") {
  if (is_win) {
    output_name = "initialexe/radium"
  } else {
    output_name = "radium"
  }

  sources = []

  public_deps = []
  deps = [ "//base" ]

  data = [ "$root_out_dir/radium_resources.pak" ]
  data_deps = []
  defines = []

  if (is_win) {
    sources += [
      "app/delay_load_failure_hook_win.cc",
      "app/delay_load_failure_hook_win.h",
      "app/main_dll_loader_win.cc",
      "app/main_dll_loader_win.h",
      "app/radium_exe_main_win.cc",
    ]

    deps += [
      ":radium_dll",
      "//components/crash/core/app",
      "//components/crash/core/app:run_as_crashpad_handler",
      "//content:sandbox_helper_win",
      "//content/public/common:static_switches",
      "//radium/app:exit_code_watcher",
      "//radium/common:constants",
      "//radium/common/win:delay_load_failure_support",
      "//radium/install_static:secondary_module",
      "//radium/radium_elf",
      "//sandbox",
      "//sandbox/policy",
      "//sandbox/policy/mojom",
      "//third_party/crashpad/crashpad/util",
    ]

    if (win_console_app) {
      defines += [ "WIN_CONSOLE_APP" ]
    } else {
      # Set /SUBSYSTEM:WINDOWS for chrome.exe itself, unless a console build
      # has been requested.
      configs -= [ "//build/config/win:console" ]
      configs += [ "//build/config/win:windowed" ]
    }

    configs += [
      "//build/config/win:delayloads",
      "//build/config/win:delayloads_not_for_child_dll",
    ]

    if (current_cpu == "x86") {
      # Set the initial stack size to 0.5MiB, instead of the 1.5MiB needed by
      # Chrome's main thread. This saves significant memory on threads (like
      # those in the Windows thread pool, and others) whose stack size we can
      # only control through this setting. Because Chrome's main thread needs
      # a minimum 1.5 MiB stack, the main thread (in 32-bit builds only) uses
      # fibers to switch to a 1.5 MiB stack before running any other code.
      ldflags = [ "/STACK:0x80000" ]
    } else {
      # Increase the initial stack size. The default is 1MB, this is 8MB.
      ldflags = [ "/STACK:0x800000" ]
    }
  } else if (use_aura) {
    sources += [ "app/radium_exe_main_aura.cc" ]
  }

  if (is_linux) {
    sources += [
      "app/radium_dll_resource.h",
      "app/radium_main.cc",
      "app/radium_main_delegate.cc",
      "app/radium_main_delegate.h",
      "app/startup_timestamps.h",
    ]

    deps += [
      ":dependencies",
      "//content/public/app",
      "//radium/common",
      "//radium/common:version_header",
    ]

    if (use_ozone) {
      deps += [ "//ui/ozone" ]
      if (is_linux) {
        deps += [ "//ui/linux:display_server_utils" ]
      }
    }
  }

  # These files are used by the installer so we need a public dep.
  public_deps += [ ":packed_resources" ]

  # The step's output are needed at runtime, so we also need a data_dep.
  data_deps += [ ":packed_resources" ]
}

if (is_win) {
  # This manifest matches what GYP produced. It may not even be necessary.
  windows_manifest("radium_dll_manifest") {
    sources = [
      as_invoker_manifest,
      common_controls_manifest,
    ]
  }

  shared_library("radium_dll") {
    configs += [ "//build/config/compiler:wexit_time_destructors" ]
    configs -= [ "//build/config/compiler:thinlto_optimize_default" ]
    configs += [ "//build/config/compiler:thinlto_optimize_max" ]

    output_name = "radium"

    sources = [
      "//base/win/dllmain.cc",
      "app/radium_main.cc",
      "app/radium_main_delegate.cc",
      "app/radium_main_delegate.h",
      "app/startup_timestamps.h",
    ]

    deps = [
      ":dependencies",
      ":radium_dll_manifest",
      "//components/crash/core/app",
      "//components/crash/core/app:crash_export_thunks",
      "//components/crash/core/common:crash_key",
      "//content/public/app",
      "//radium/child",
      "//radium/radium_elf",
    ]

    configs += [ "//build/config/win:delayloads" ]
  }
}

group("dependencies") {
  public_deps = [
    "//components/content_settings/core/common",
    "//components/crash/core/app",
    "//components/memory_system",
    "//components/startup_metric_utils",
    "//radium/browser",
    "//radium/common",
    "//radium/renderer",
  ]
}

if (!is_android) {
  radium_paks("packed_resources") {
    if (is_mac) {
      output_dir = "$root_gen_dir/repack"
      copy_data_to_bundle = true
    } else {
      output_dir = root_out_dir
      mark_as_data = true
    }

    if (enable_resource_allowlist_generation) {
      repack_allowlist = _radium_resource_allowlist
      deps = [ ":resource_allowlist" ]
    }

    # This needs to be in-sync with //radium/app/packed_resources_integrity.h.
    files_to_hash = [
      "radium_resources.pak",
      "radium_100_percent.pak",
    ]
    if (enable_hidpi) {
      files_to_hash += [ "radium_200_percent.pak" ]
    }
  }

  # # This is extracted to deserialize build dependency around
  # # :packed_resources_integrity_hash and improve build parallelism.
  # source_set("packed_resources_integrity_header") {
  #   sources = [ "app/packed_resources_integrity.h" ]

  #   # chrome/app/packed_resources_integrity.cc file is generated in dependency.
  #   deps = [ ":packed_resources_integrity" ]
  # }
}

# Android also supports this, but uses
# //chrome/android:${_variant}_resource_allowlist.
if (is_win && enable_resource_allowlist_generation) {
  generate_resource_allowlist("resource_allowlist") {
    deps = [ ":radium_dll" ]
    inputs = [ "$root_out_dir/radium.dll.pdb" ]
    output = _radium_resource_allowlist
  }
}
