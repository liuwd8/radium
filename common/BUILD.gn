# Copyright 2024 The Radium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/util/process_version.gni")

group("common") {
  public_deps = [ ":common_lib" ]
}

source_set("channel_info") {
  sources = [
    "channel_info.cc",
    "channel_info.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
  public_deps = [
    "//base",
    "//components/version_info",
    "//components/version_info:version_string",
  ]
}

static_library("common_lib") {
  visibility = [ ":*" ]

  sources = [
    "logging_radium.cc",
    "logging_radium.h",
    "radium_content_client.cc",
    "radium_content_client.h",
    "radium_paths.cc",
    "radium_paths.h",
    "radium_paths_internal.h",
    "radium_result_codes.cc",
    "radium_result_codes.h",
  ]

  deps = [
    ":channel_info",
    ":constants",
    ":url_constants",
    ":version_header",
    "//base",
    "//components/crash/core/common",
    "//content/public/common",
    "//radium/common/profiler",
  ]
}

process_version("version_header") {
  # TODO(brettw) this should have more reduced visibility, but chrome/browser
  # currently depends on this.
  #visibility = [ ":*" ]

  sources = [
    "//chrome/VERSION",
    "//radium/BRANDING",
  ]

  template_file = "radium_version.h.in"

  output = "$target_gen_dir/radium_version.h"
}

# Separate out the constants that aren't intermingled with or dependent on code
# so that they can be used without adding any binary bloat.
static_library("non_code_constants") {
  sources = [
    "radium_constants.cc",
    "radium_constants.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [ "//base" ]

  public_deps = [ "//ui/base:buildflags" ]
}

component("radium_features") {
  defines = [ "IS_RADIUM_FEATURES_IMPL" ]

  sources = [
    "radium_features.cc",
    "radium_features.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    ":non_code_constants",
    "//base",
  ]

  public_deps = [ "//ui/base:buildflags" ]
}

static_library("url_constants") {
  visibility = [ "//radium/common/*" ]
  sources = [
    "webui_url_constants.cc",
    "webui_url_constants.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    ":constants",
    "//base",
  ]
}

static_library("constants") {
  sources = [
    "pref_names.h",
    "radium_paths.cc",
    "radium_paths.h",
    "radium_paths_internal.h",
    "radium_result_codes.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    ":non_code_constants",
    ":radium_features",
    "//base",
    "//build:branding_buildflags",
    "//components/policy/core/common:policy_path_constants",
  ]

  if (is_win) {
    sources += [ "radium_paths_win.cc" ]
  }

  if (is_linux) {
    sources += [ "radium_paths_linux.cc" ]
  }

  if (is_mac) {
    sources += [ "radium_paths_mac.mm" ]
  }
}
