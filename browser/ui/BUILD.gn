# Copyright 2024 The Radium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//crypto/features.gni")

static_library("ui") {
  public = [
    "browser.h",
    "browser_finder.h",
    "browser_list_enumerator.h",
    "simple_message_box.h",
    "startup/startup_browser_creator.h",
    "ui_features.h",
  ]

  sources = [
    "browser.cc",
    "browser_finder.cc",
    "browser_list_enumerator.cc",
    "simple_message_box_internal.cc",
    "simple_message_box_internal.h",
    "startup/startup_browser_creator.cc",
    "ui_features.cc",
  ]

  public_deps = []
  deps = [
    "//base",
    "//components/ui_devtools",
    "//radium/browser/ui/color",
    "//radium/browser/ui/prefs:impl",
    "//radium/browser/ui/signin",
    "//radium/browser/ui/tab_contents",
    "//radium/browser/ui/webui",
    "//third_party/inspector_protocol:crdtp",
  ]

  if (!is_android) {
    public += [ "browser_list.h" ]

    sources += [
      "browser_list.cc",
      "browser_list_observer.h",
      "browser_observer.h",
      "browser_user_data.h",
      "browser_window.cc",
      "browser_window.h",
      "unload_controller.cc",
      "unload_controller.h",
    ]

    deps += [ "//radium/browser/tab_contents" ]
  }

  if (toolkit_views) {
    sources += [
      "views/accessibility/non_accessible_image_view.cc",
      "views/accessibility/non_accessible_image_view.h",
      "views/controls/hover_button.cc",
      "views/controls/hover_button.h",
      "views/controls/rich_hover_button.cc",
      "views/controls/rich_hover_button.h",
      "views/devtools_process_observer.cc",
      "views/devtools_process_observer.h",
      "views/hover_button_controller.cc",
      "views/hover_button_controller.h",
      "views/message_box_dialog.cc",
      "views/message_box_dialog.h",
      "views/radium_browser_main_extra_parts_views.cc",
      "views/radium_browser_main_extra_parts_views.h",
      "views/radium_constrained_window_views_client.cc",
      "views/radium_constrained_window_views_client.h",
      "views/radium_layout_provider.cc",
      "views/radium_layout_provider.h",
      "views/radium_views_delegate.cc",
      "views/radium_views_delegate.h",
      "window_finder.cc",
      "window_finder.h",
    ]

    public_deps += [ "//ui/base/mojom:ui_base_types" ]

    deps += [
      "//components/constrained_window",
      "//components/ui_devtools/views",
      "//radium/browser/ui/gallery",
      "//radium/browser/ui/views/dragging",
      "//ui/views",
      "//ui/views:buildflags",
    ]

    if (is_linux) {
      sources += [
        "views/process_singleton_dialog_linux.cc",
        "views/radium_views_delegate_linux.cc",
      ]
    } else if (is_win) {
      sources += [ "views/radium_views_delegate_win.cc" ]
    } else if (is_mac) {
      sources += [
        "cocoa/l10n_util.h",
        "cocoa/l10n_util.mm",
        "cocoa/simple_message_box_cocoa.h",
        "cocoa/simple_message_box_cocoa.mm",
        "views/radium_views_delegate_mac.cc",
      ]

      deps += [ "//content/public/browser" ]
    }

    if (is_win || is_linux) {
      sources += [
        "views/native_widget_factory.cc",
        "views/native_widget_factory.h",
      ]
    }

    if (use_aura && is_linux) {
      sources += [
        "views/radium_browser_main_extra_parts_views_linux.cc",
        "views/radium_browser_main_extra_parts_views_linux.h",
      ]

      deps += [
        "//ui/base/cursor",
        "//ui/ozone",
      ]

      if (use_dbus) {
        sources += [
          "views/dark_mode_manager_linux.cc",
          "views/dark_mode_manager_linux.h",
        ]
        deps += [
          "//components/dbus",
          "//dbus",
        ]
      }
    }

    if (use_nss_certs) {
      sources += [
        "crypto_module_delegate_nss.cc",
        "crypto_module_delegate_nss.h",
      ]
    }
  }
}
