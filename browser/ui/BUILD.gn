# Copyright 2024 The Radium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//crypto/features.gni")

static_library("ui") {
  public = [
    "startup/startup_browser_creator.h",
    "ui_features.h",
  ]

  sources = [
    "browser.h",
    "startup/startup_browser_creator.cc",
    "ui_features.cc",
  ]

  public_deps = []
  deps = [
    "//base",
    "//components/ui_devtools",
    "//radium/browser/ui/color",
    "//radium/browser/ui/signin",
    "//third_party/inspector_protocol:crdtp",
  ]

  if (toolkit_views) {
    sources += [
      "views/devtools_process_observer.cc",
      "views/devtools_process_observer.h",
      "views/radium_browser_main_extra_parts_views.cc",
      "views/radium_browser_main_extra_parts_views.h",
      "views/radium_constrained_window_views_client.cc",
      "views/radium_constrained_window_views_client.h",
      "views/radium_layout_provider.cc",
      "views/radium_layout_provider.h",
      "views/radium_views_delegate.cc",
      "views/radium_views_delegate.h",
    ]

    public_deps += [ "//ui/base/mojom:ui_base_types" ]

    deps += [
      "//components/constrained_window",
      "//components/ui_devtools/views",
      "//ui/views",
      "//ui/views:buildflags",
    ]

    if (is_linux) {
      sources += [ "views/radium_views_delegate_linux.cc" ]
    }

    if (is_win || is_linux) {
      sources += [
        "views/native_widget_factory.cc",
        "views/native_widget_factory.h",
      ]
    }

    if (use_aura && is_linux) {
      sources += [
        "views/radium_browser_main_extra_parts_views_linux.cc",
        "views/radium_browser_main_extra_parts_views_linux.h",
      ]

      deps += [
        "//ui/base/cursor",
        "//ui/ozone",
      ]

      if (use_dbus) {
        sources += [
          "views/dark_mode_manager_linux.cc",
          "views/dark_mode_manager_linux.h",
        ]
        deps += [
          "//components/dbus/thread_linux",
          "//dbus",
        ]
      }
    }

    if (use_nss_certs) {
      sources += [
        "crypto_module_delegate_nss.cc",
        "crypto_module_delegate_nss.h",
      ]
    }
  }
}
