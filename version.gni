# Copyright 2024 The Chromium Radium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Give version.py a pattern that will expand to a GN scope consisting of
# all values we need at once.
_version_dictionary_template = "full = \"@MAJOR@.@MINOR@.@BUILD@.@PATCH@\" " +
                               "major = \"@MAJOR@\" minor = \"@MINOR@\" " +
                               "build = \"@BUILD@\" patch = \"@PATCH@\" "

# The file containing the Chrome version number.
radium_version_file = "//radium/VERSION"

_script_arguments = []

if (target_os == "android") {
  import("//build/config/android/config.gni")

  _version_dictionary_template +=
      "radium_version_code = " + "\"@RADIUM_VERSION_CODE@\" "

  if (target_cpu == "arm64" || target_cpu == "x64") {
    _version_dictionary_template +=
        "radium_32_version_code = \"@RADIUM_32_VERSION_CODE@\" "
  }

  _script_arguments += [
    "-a",
    target_cpu,
  ]

  if (defined(final_android_sdk) && !final_android_sdk) {
    _script_arguments += [ "--next" ]
  }
}

_script_arguments += [
  "-f",
  rebase_path(radium_version_file, root_build_dir),
  "-t",
  _version_dictionary_template,
  "--os",
  target_os,
]

_result = exec_script("//build/util/version.py",
                      _script_arguments,
                      "scope",
                      [
                        radium_version_file,
                        "//build/util/android_chrome_version.py",
                      ])

# Full version. For example "45.0.12321.0"
radium_version_full = _result.full

# The constituent parts of the full version.
radium_version_major = _result.major
radium_version_minor = _result.minor
radium_version_build = _result.build
radium_version_patch = _result.patch

if (target_os == "android") {
  forward_variables_from(_result,
                         [
                           "radium_version_code",
                           "radium_32_version_code",
                         ])

  radium_version_name = radium_version_full
}
