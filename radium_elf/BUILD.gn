# Copyright 2024 The Radium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/win/manifest.gni")
import("//radium/process_version_rc_template.gni")

process_version_rc_template("radium_elf_resources") {
  sources = [ "radium_elf.ver" ]
  output = "$target_gen_dir/radium_elf_version.rc"
}

# This manifest matches what GYP produces. It may not even be necessary.
windows_manifest("radium_elf_manifest") {
  sources = [ as_invoker_manifest ]
}

# This target contains utility functions which must only depend on
# kernel32 or ntdll. Please don't add dependencies on other system libraries.
static_library("nt_registry") {
  sources = [
    "nt_registry/nt_registry.cc",
    "nt_registry/nt_registry.h",
    "nt_registry/nt_registry_functions.h",
  ]

  # raw_ptr check
  configs -= [ "//build/config/clang:find_bad_constructs" ]

  libs = [
    "kernel32.lib",
    "ntdll.lib",
  ]
}

static_library("crash") {
  sources = [
    "../app/radium_crash_reporter_client_win.cc",
    "../app/radium_crash_reporter_client_win.h",
    "../common/radium_result_codes.h",
    "crash/crash_helper.cc",
    "crash/crash_helper.h",
  ]
  deps = [
    ":constants",
    "//base",  # This needs to go.  DEP of app, crash_keys, client.
    "//base:base_static",  # pe_image
    "//components/crash/core/app",
    "//components/crash/core/common",  # crash_keys
    "//components/version_info:channel",
    "//content/public/common:result_codes",
    "//sandbox/policy:win_hook_util",
    "//third_party/crashpad/crashpad/client",  # DumpWithoutCrash

    # "//radium/install_static:install_static_util",
  ]
}

source_set("pe_image_safe") {
  sources = [
    "pe_image_safe/pe_image_safe.cc",
    "pe_image_safe/pe_image_safe.h",
  ]
}

source_set("security") {
  sources = [
    "radium_elf_security.cc",
    "radium_elf_security.h",
  ]
  deps = [
    ":constants",
    ":nt_registry",
    "//base:base",
    "//radium/install_static:install_static_util",
  ]
}

source_set("sha1") {
  sources = [
    "sha1/sha1.cc",
    "sha1/sha1.h",
  ]
}

source_set("third_party_dlls") {
  visibility = [ ":*" ]  # Only targets in this file can depend on this.
  sources = [
    "third_party_dlls/beacon.cc",
    "third_party_dlls/beacon.h",
    "third_party_dlls/hardcoded_blocklist.cc",
    "third_party_dlls/hardcoded_blocklist.h",
    "third_party_dlls/hook.cc",
    "third_party_dlls/hook.h",
    "third_party_dlls/logs.cc",
    "third_party_dlls/logs.h",
    "third_party_dlls/main.cc",
    "third_party_dlls/main.h",
    "third_party_dlls/packed_list_file.cc",
    "third_party_dlls/packed_list_file.h",
  ]
  deps = [
    "//base:base_static",
    "//sandbox/win:service_resolver",
  ]
  public_deps = [
    ":constants",
    ":crash",
    ":nt_registry",
    ":pe_image_safe",
    ":sha1",
    ":third_party_shared_defines",
    "//radium/install_static:install_static_util",
    "//sandbox/policy:win_hook_util",
  ]
}

# This source_set defines third-party-related structures and APIs used from
# outside chrome_elf.dll.  The APIs are exported from chrome_elf (add a
# data_dep on //chrome/chrome_elf:chrome_elf), which will always be loaded
# before chrome.dll.
source_set("third_party_shared_defines") {
  sources = [
    "sha1/sha1.h",
    "third_party_dlls/packed_list_format.cc",
    "third_party_dlls/packed_list_format.h",
    "third_party_dlls/public_api.cc",
    "third_party_dlls/public_api.h",
    "third_party_dlls/status_codes.cc",
    "third_party_dlls/status_codes.h",
  ]
}

source_set("constants") {
  sources = [
    "blocklist_constants.cc",
    "blocklist_constants.h",
    "radium_elf_constants.cc",
    "radium_elf_constants.h",
  ]
}

shared_library("radium_elf") {
  sources = [
    "radium_elf.def",
    "radium_elf_main.cc",
    "radium_elf_main.h",
  ]

  deps = [
    ":constants",
    ":crash",
    ":nt_registry",
    ":pe_image_safe",
    ":radium_elf_manifest",
    ":radium_elf_resources",
    ":security",
    ":third_party_dlls",
    "//components/crash/core/app:crash_export_thunks",
    "//radium/install_static:install_static_util",
    "//radium/install_static:primary_module",
    "//sandbox/policy:win_hook_util",
  ]

  configs += [ "//build/config/win:windowed" ]
  configs -= [ "//build/config/win:console" ]

  configs += [
    "//build/config/win:delayloads",
    "//build/config/win:delayloads_not_for_child_dll",
  ]

  if (current_cpu == "x86") {
    # Don"t set an x64 base address (to avoid breaking HE-ASLR).
    ldflags = [ "/BASE:0x01c20000" ]
  }
}
